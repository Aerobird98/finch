' create the main factory object
Complex <- Object copyWith: {
    ' the prototypical complex number
    _prototype <- Object copyWith: {
        _real <- 0
        _imaginary <- 0
        
        ' accessors
        self :: real { _real }
        self :: imaginary { _imaginary }
        
        ' string conversion
        self :: toString {
            _real toString + "+" + _imaginary + "i"
        }
        
        ' arithmetic
        self :: + right {
            Complex new: _real + right real i: _imaginary + right imaginary
        }

        self :: - right {
            Complex new: _real - right real i: _imaginary - right imaginary
        }

        self :: * right {
            Complex new: (_real * right real) - (_imaginary * right imaginary) i:
                         (_imaginary * right real) + (_real * right imaginary) 
        }
        
        self :: magnitude {
            ((_real * _real) + (_imaginary * _imaginary)) sqrt
        }
    }
    
    ' accessor for the prototype
    self :: prototype { _prototype }
    
    ' constructor
    self :: new: real i: imaginary {
        _prototype copyWith: {
            _real <- real
            _imaginary <- imaginary
        }
    }
}

' allow creating complex numbers from normal ones
Number prototype :: i { Complex new: 0 i: self }

' test it out a bit
a <- 4 i
b <- Complex new: 2 i: 3

writeLine: "a = " + a
writeLine: "b = " + b
writeLine: "|b| = " + b magnitude
writeLine: "a + b = " + (a + b)
writeLine: "a * b = " + (a * b)
