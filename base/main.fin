' create the factory object
New <- Object copyWith: {
    _array <- Object copyWith: {
        self addMethod: "count:" body: {
            |count|
            Primitives__ newArrayCount: count
        }

        self addMethod: "count:fillWith:" body: {
            |count element|
            result <- []

            from: 1 to: count do: {|i| result add: element }
            result
        }
    }
    
    self addMethod: "array" body: { _array }
}

' define a for-style loop
Ether addMethod: "from:to:step:do:" body: {
    |start end step block|
    i <- start
    
    while: { i <= end } do: {
        block call: i
        i <-- i + step
    }
}

Ether addMethod: "from:to:do:" body: {
    |start end block|
    from: start to: end step: 1 do: block
}

' a chained if/then/else
Ether addMethod: "if:then:elseif:then:else:" body: {
    |if1 then1 if2 then2 else|
    if: if1 then: then1 else: {
        if: if2 then: then2 else: else
    }
}

' array methods
Array addMethod: "each:" body: {|block|
    from: 0 to: self length - 1 do: {|i|
        block call: (self at: i)
    }
}

Array addMethod: "map:" body: {|block|
    result <- []
    self each: {|element| result add: (block call: element) }
    result
}

writeLine: "Base library loaded."