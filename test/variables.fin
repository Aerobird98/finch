Test suite: "Variables" is:
{
    Test test: "Define" is:
    {
        def a <- "something"
        Test that: a equals: "something"
    }
    
    '### bob: what should have if you define the same variable name
    '         in the same scope twice?
    
    Test test: "Undefine" is:
    {
        def a <- "something"
        Test that: a equals: "something"
        undef a
        Test that: a equals: Nil
        a <- "else"
        Test that: a equals: Nil
    }
    
    Test test: "Undefine Unshadows" is:
    {
        def a <- "outer"
        
        do:
        {
            def a <- "inner"
            Test that: a equals: "inner"
            undef a
            ' should see parent scoped a now
            Test that: a equals: "outer"
        }
    }
    
    Test test: "Assignment" is:
    {
        def a <- "before"
        Test that: a equals: "before"
        a <- "after"
        Test that: a equals: "after"
    }
    
    Test test: "Assign Undefined" is:
    {
        a <- "something"
        ' since a wasn't defined, should do nothing
        Test that: a equals: Nil
    }

    Test test: "Local Scope" is:
    {
        do:
        {
            def a <- "local"
            Test that: a equals: "local"
        }
        
        Test that: a equals: Nil
    }

    Test test: "Nested Local Scope" is:
    {
        def a <- "local"
        
        do: { Test that: a equals: "local" }

        Test that: a equals: "local"
    }

    Test test: "Shadowed Local Scope" is:
    {
        def a <- "outer"
        
        do:
        {
            def a <- "inner"
            Test that: a equals: "inner"
        }

        Test that: a equals: "outer"
    }
    
    Test test: "Object Scope" is:
    {
        def foo <- Object clone
        
        foo addMethod: "createVar" body:
        {
            def _someVar <- "obj var"
        }
        
        foo addMethod: "testVar" body:
        {
            Test that: _someVar equals: "obj var"
        }
        
        Test that: _someVar equals: Nil
        foo createVar
        Test that: _someVar equals: Nil
        foo testVar
    }
    
    Test test: "Global Scope" is:
    {
        do:
        {
            def SomeGlobal <- "global"
            Test that: SomeGlobal equals: "global"
        }
        
        Test that: SomeGlobal equals: "global"
    }
}