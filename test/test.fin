writeLine: "Running Finch test suite."

' define a Test object the tests can use
Test <- Object copyWith: {
    _suite      <- ""
    _test       <- ""
    _testShown  <- False
    _tests      <- 0
    _failed     <- 0

    _totalTests <- 0
    _totalFails <- 0
}

Test :: (
    suite: name is: body {
        _suite <- name
        body call
        _suite <- ""
    }

    test: name is: body {
        _test       <- name
        _tests      <- 0
        _failed     <- 0
        _testShown  <- False

        body call

        if: _failed > 0 then: {
            writeLine: "XXX " + _suite + " / " + _test + " failed " + _failed +
                " / " + _tests + " tests"
        }
        _test <- ""
    }

    showHeader {
        if: _testShown not then: {
            writeLine: _suite + " / " + _test
            _testShown <- True
        }
    }

    fail { self isTrue: False }
    pass { self isTrue: True }

    isTrue: actual {
        _tests <- _tests + 1
        _totalTests <- _totalTests + 1

        if: actual not then: {
            _failed <- _failed + 1
            _totalFails <- _totalFails + 1

            self showHeader
            writeLine: "  Test " + _tests toString + " failed."
        }
    }

    isNil: actual { self that: actual equals: Nil }

    isFalse: actual { self isTrue: actual not }

    that: actual equals: expected {
        self isTrue: actual = expected
    }

    complete {
        writeLine: ""
        if: _totalFails = 0 then: {
            writeLine: "All " + _totalTests + " tests passed."
        } else: {
            writeLine: _totalFails toString + " out of " + _totalTests +
                " tests failed."
        }
        writeLine: ""
    }
)

' run the test scripts
'### bob: hack! path should be relative to this script's path!
load: "../../test/arithmetic.fin"
load: "../../test/arrays.fin"
load: "../../test/booleans.fin"
load: "../../test/cascade.fin"
load: "../../test/fibers.fin"
load: "../../test/literals.fin"
load: "../../test/messages.fin"
load: "../../test/objects.fin"
load: "../../test/self.fin"
load: "../../test/strings.fin"
load: "../../test/switch.fin"
load: "../../test/tco.fin"
load: "../../test/variables.fin"

Test complete
