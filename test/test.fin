writeLine: "Running Finch test suite."

' define a Test object the tests can use
Test <- Object copyWith: {
    _suite      <- ""
    _test       <- ""
    _testShown  <- False
    _tests      <- 0
    _failed     <- 0
    
    self :: suite: name is: body {
        _suite <- name
        body call
        _suite <- ""
    }

    self :: test: name is: body {
        _test       <- name
        _tests      <- 0
        _failed     <- 0
        _testShown  <- False

        body call

        header <- "--- "
        if: _failed > 0 then: { header <-- "XXX " }

        writeLine: header + _suite + " / " + _test + " " + (_tests - _failed) +
            " / " + _tests + " tests passed"

        _test <- ""
    }

    self :: showHeader {
        if: _testShown not then: {
            writeLine: _suite + " / " + _test
            _testShown <- True
        }
    }

    self :: isTrue: actual {
        _tests <- _tests + 1

        if: actual not then: {
            _failed <- _failed + 1
            self showHeader
            writeLine: "  Test " + _tests toString + " failed."
        }
    }

    self :: isFalse: actual { self isTrue: actual not }

    self :: that: actual equals: expected {
        self isTrue: actual = expected
    }
}

' run the test scripts
'### bob: hack! path should be relative to this script's path!
load: "../../test/arrays.fin"
load: "../../test/booleans.fin"
load: "../../test/arithmetic.fin"
load: "../../test/literals.fin"
load: "../../test/variables.fin"
load: "../../test/fibers.fin"
